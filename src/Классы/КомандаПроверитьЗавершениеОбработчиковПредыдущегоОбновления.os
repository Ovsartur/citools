///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды help
//
///////////////////////////////////////////////////////////////////////////////

#Использовать logos
#Использовать asserts
#Использовать v8storage
#Использовать tempfiles

Перем Лог;

Процедура НастроитьКоманду(Знач Команда, Знач Парсер) Экспорт

	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ИмяБазы", "Имя информационной базы, которую нужно проверить");
	
КонецПроцедуры // НастроитьКоманду

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   Приложение - Модуль - Модуль менеджера приложения
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач Приложение) Экспорт
	
	Лог = Приложение.ПолучитьЛог();
	Лог.Информация("Выполняется проверка завершенности обработчиков обновления");
	
	ИмяБазы = ПараметрыКоманды["ИмяБазы"];
	Если ПараметрыКоманды["ИмяБазы"] = Неопределено Тогда
		Приложение.ЗавершитьРаботуПриложенияСОшибкой("Попытка выполнения команды без указания имени базы");
	КонецЕсли;
		
	// Получить версию релизного хранилища задеплоиного в рабочую базу
	СтрокаСоединенияСРабочейБазой = ОбщиеМетоды.ПолучитьСтрокуСоединенияСРабочейБазой(ПараметрыКоманды);
	ПараметрыАвторизацииРабочаяБаза = ОбщиеМетоды.ПолучитьПараметрыАвторизацииВРабочейБазе(ПараметрыКоманды);
		
	Лог.Отладка("Устанавливаю COM соединение с рабочей базой для проверки завершения отложенных обработчиков предыдущего обновления");
	COMСоединение = ОбщиеМетоды.ПолучитьCOMСоединениеСБазой(ПараметрыКоманды, СтрокаСоединенияСРабочейБазой);
		
	Если COMСоединение = Неопределено Тогда
		ВызватьИсключение "Не удалось установить создать com-подключение к базе с переданными параметрами";	
	КонецЕсли;

	ПредыдущиеОбработчикиЗавершены = COMСоединение.реми_Деплой.ЗавершеныОбработчикиПредыдущегоОбновления();
	ОсвободитьОбъект(COMСоединение);
	COMСоединение = Неопределено;
	ВыполнитьСборкуМусора();

	Если НЕ ПредыдущиеОбработчикиЗавершены Тогда
		// TODO Выяснить нужно ли продолжить деплой до обновления основной конфигурации БД включительно
		Приложение.ЗавершитьРаботуПриложенияСОшибкой("Не завершены обработчики обновления. Обновление остановлено");
	Иначе
		Лог.Отладка("Все обработчики предыдущего обновления успешно завершены");
	КонецЕсли;
	
	Возврат Приложение.РезультатыКоманд().Успех;
		
КонецФункции // ВыполнитьКоманду
