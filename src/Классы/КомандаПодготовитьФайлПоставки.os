///////////////////////////////////////////////////////////////////////////////
//
// Служебный модуль с реализацией работы команды
//
///////////////////////////////////////////////////////////////////////////////

#Использовать logos
#Использовать asserts
#Использовать v8storage
#Использовать v8runner
#Использовать tempfiles

Перем Лог;

Процедура НастроитьКоманду(Знач Команда, Знач Парсер) Экспорт
	
	Парсер.ДобавитьПозиционныйПараметрКоманды(Команда, "ИмяБазы", "Имя информационной базы, для которой создать файлы поставки");
	
КонецПроцедуры // НастроитьКоманду

// Выполняет логику команды
// 
// Параметры:
//   ПараметрыКоманды - Соответствие - Соответствие ключей командной строки и их значений
//   Приложение - Модуль - Модуль менеджера приложения
//
Функция ВыполнитьКоманду(Знач ПараметрыКоманды, Знач Приложение) Экспорт

	Лог = Приложение.ПолучитьЛог();
	Лог.Информация("Выполняется создание файла поставки");

	КаталогСборки = ПараметрыКоманды.Получить("TEMP_WORKDIR");
	ИнформационнаяБаза = ПараметрыКоманды["ИмяБазы"];
	Если ИнформационнаяБаза = Неопределено Тогда
		Приложение.ЗавершитьРаботуПриложенияСОшибкой("Попытка выполнения команды без указания имени базы");
	КонецЕсли;

	КаталогВременныхФайловСборки = ОбъединитьПути(КаталогСборки, ИнформационнаяБаза);
	ПутьКФайлу = ОбъединитьПути(КаталогВременныхФайловСборки, "versions");

	СтруктураДанных = ОбщиеМетоды.ЗагрузитьФайлВСтруктуру(ПутьКФайлу);

	Если СтруктураДанных = Неопределено Тогда
		Приложение.ЗавершитьРаботуПриложенияСОшибкой("Отсутствует временный файл versions");
	КонецЕсли;

	НомерВерсииХранилища = СтруктураДанных.ВерсияХранилища;
	Если НЕ ЗначениеЗаполнено(НомерВерсииХранилища) Тогда
		Приложение.ЗавершитьРаботуПриложенияСОшибкой("Не удалось определить версию хранилища для деплоя");
	КонецЕсли;
	Лог.Отладка(СтрШаблон("Будет создан файл поставки по версии хранилища - ""%1""", НомерВерсииХранилища));

	ПутьКФайлуКонфигурации = ОбъединитьПути(КаталогВременныхФайловСборки, Строка(НомерВерсииХранилища) + ".cf");
	Попытка
		ОбщиеМетоды.ВыгрузитьВерсиюКонфигурацииИзХранилища(ПараметрыКоманды, ПутьКФайлуКонфигурации, НомерВерсииХранилища);
	Исключение
		Приложение.ЗавершитьРаботуПриложенияСОшибкой(СтрШаблон("Ошибка выгрузки хранилища в файл. Описание: %1", ОписаниеОшибки()));
	КонецПопытки;
	Лог.Отладка(СтрШаблон("Создан файл конфигурации хранилища - %1", ПутьКФайлуКонфигурации));

	ПутьКФайлуПоставки = ОбъединитьПути(КаталогВременныхФайловСборки, "1Cv8.cf");
	Попытка
		ОбщиеМетоды.СоздатьФайлПоставкиИзФайлаКонфигурации(ПутьКФайлуКонфигурации, ПутьКФайлуПоставки);
	Исключение
		Приложение.ЗавершитьРаботуПриложенияСОшибкой(СтрШаблон("Файл поставки не создан. Описание: %1", ОписаниеОшибки()));
	КонецПопытки;

	Лог.Отладка(СтрШаблон("Создан файл поставки - %1", ПутьКФайлуПоставки));
	
	Возврат Приложение.РезультатыКоманд().Успех;
	
КонецФункции // ВыполнитьКоманду